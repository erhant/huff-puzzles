
/** 
 *  MULTIPLY HUFF EXERCISE
 *
 *  When writing solidity, we most times have to perform some mathematical operations like multiplication etc
 *  
 *  The task is to write within the `MAIN` macro below, a function named `multiply` that takes in 2 uint256s, and returns their product. Be sure to revert on overflow
 *
 *  NOTICE: The contract should revert when an unrecognized function is called
 */

#define function multiply(uint256, uint256) payable returns(uint256)

#define macro MAIN() = takes(0) returns(0) {
    // get the 4-byte function selector
    0x00 calldataload 0xE0 shr

    // jump to multiply if selector matches
    __FUNC_SIG(multiply) eq multiply_func jumpi

    // revert otherwise
    0x00 0x00 revert

    multiply_func:
        // we could write the logic here, but lets use a macro :)
        MULTIPLY() 
}

#define macro MULTIPLY() = {
    0x04 calldataload   // [n]
    0x24 calldataload   // [n, m]
    dup2                // [n, m, n]
    dup2                // [n, m, n, m]
    mul                 // [n, m, k]    k = n*m
    swap2               // [k, m, n]

    // check zero
    dup3                // [k, m, n, k]
    iszero iszero       // [k, m, n, k != 0]
    continue jumpi
    pop                 // [k, m]
    pop                 // [k]
    ret jump

    // check overflow (works for non-zero results)
    continue:
                        // [k, m, n]
    dup3                // [k, m, n, k]
    div                 // [k, m, k/n]
    eq                  // [k, m == k/n]
    ret jumpi

    pop                 // []
    0x00 0x00 revert

    ret:
        0x00 mstore
        0x20 0x00 return
}
